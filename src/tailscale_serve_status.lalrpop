use std::net::Ipv4Addr;
use std::str::FromStr;

use crate::ast::{
    HttpSpec,
    HttpsSpec,
    ListenAddress,
    OperationMode,
    Protocol,
    ListenAddressWithProtocol,
    DomainOrIp,
};

grammar;


pub HttpSpecList: Vec<HttpSpec> = HttpSpec*;
pub HttpsSpecList: Vec<HttpsSpec> = HttpsSpec*;

pub HttpSpec: HttpSpec = {
    <ts_listen:HttpListenAddressLines> <port:HttpProxyTargetPort> => {
        HttpSpec {
            tailscale_listen_addresses: ts_listen,
            proxy_target_port: port,
        }
    },
};

pub HttpsSpec: HttpsSpec = {
    <ts_listen:HttpsListenAddressLines> <port:HttpProxyTargetPort> => {
        let (mode, tailscale_listen_addresses) = ts_listen;
        HttpsSpec {
            mode,
            tailscale_listen_addresses,
            proxy_target_port: port,
        }
    },
};

HttpsListenAddressLines: (OperationMode, Vec<ListenAddressWithProtocol>) = {
    (<ListenAddressWithProtocol> "(tailnet only)")+ => (OperationMode::Serve, <>),
    (<ListenAddressWithProtocol> "(Funnel on)")+ => (OperationMode::Funnel, <>),
};

HttpListenAddressLines: Vec<ListenAddressWithProtocol> = {
    (<ListenAddressWithProtocol> "(tailnet only)" "\n")+
};

HttpProxyTargetPort: u16 = "|-- / proxy http://127.0.0.1:" <PortNumber>; 

ListenAddressWithProtocol: ListenAddressWithProtocol = {
    <scheme:r"https?"> "://" <domain_or_ip:DomainOrIp> <pn:(":" <PortNumber>)?> => {
        let protocol = match scheme {
            "http" => Protocol::Http,
            "https" => Protocol::Https,
            _ => panic!("Unrecognized scheme: {}", scheme),
        };

        let port = if let Some(port) = pn { port } else { protocol.default_port() };

        ListenAddressWithProtocol {
            protocol,
            listen_address: ListenAddress {
                domain_or_ip,
                port,
            },
        }
    }
};

DomainOrIp: DomainOrIp = {
    // IPv4
    #[precedence(level="0")]
    <Ipv4> => DomainOrIp::Ipv4(<>),

    // IPv6
    r"\[[0-9a-f:]+\]" => DomainOrIp::Ipv6,

    // Domain name
    <dn:r"(?:[a-z0-9](?:[a-z0-9-]{0,61}[a-z0-9])?\.)+[a-z]+"> => {
        DomainOrIp::Domain { domain_name: dn.to_string() }
    },
};


Ipv4: Ipv4Addr = r"[0-9]{1,3}[.][0-9]{1,3}[.][0-9]{1,3}[.][0-9]{1,3}"
    => {
        let mut slices = <>.split('.');
        Ipv4Addr::new(
            u8::from_str(slices.next().unwrap()).unwrap(),
            u8::from_str(slices.next().unwrap()).unwrap(),
            u8::from_str(slices.next().unwrap()).unwrap(),
            u8::from_str(slices.next().unwrap()).unwrap(),
        )
    };

PortNumber: u16 = r"[1-9][0-9]{0,4}" => u16::from_str(<>).unwrap();
